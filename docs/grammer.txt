Pylox grammer

expression     → literal
               | unary
               | binary
               | grouping ;

literal        → NUMBER | STRING | "true" | "false" | "nil" ;
grouping       → "(" expression ")" ;
unary          → ( "-" | "!" ) expression ;
binary         → expression operator expression ;
operator       → "==" | "!=" | "<" | "<=" | ">" | ">="
               | "+"  | "-"  | "*" | "/" ;


Precedence and Associativity

Precedence from lowest to highest in te table below.

Name           Operators       Associates
Comma           ,               Left
Ternary         ?:              Right
Equality        == !=           Left
Comparison      > >= < <=	    Left
Term	        - +	            Left
Factor	        / *	            Left
Unary	        ! -	            Right


Enhanced grammer (taking into account precedence and associativity) (eliminates prev.grammers's ambiguity)

Each rule here only matches expressions at its precedence level or higher.

program         -> declaration* EOF ;
declaration     -> var_decl | statement ;
var_decl        -> "var" IDENTIFIER ( "=" expression )? ";" ;
statement       -> expr_stmt | print_stmt | block | if_stmt;
if_stmt         -> "if" "(" expression ")" statement
                   ( "else" statement )? ;
block           -> "{" declaration* "}" ;
expr_stmt       -> expression ";" ;
print_stmt      -> "print" expression ";" ;

expression      -> comma ;
comma           -> assignmment ( "," assignmment )* ;
assignmment     -> IDENTIFIER "=" assignmment | ternary ;
ternary         -> equality ( "?" equality ":" ternary )* ;
equality        -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison      -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term            -> factor ( ( "-" | "+" ) factor )* ;
factor          -> factor ("/" | "*") unary | unary ; # left recursive not good for parsing technique to be used
factor          -> unary (("/" | "*") unary)* ;
unary           -> ("-" | "!") unary | primary;
unary           -> ("-" | "!" | "!=" | "==" | ">" | ">=" | "<" | "<=" |"/" | "*" | "+") unary | primary; # error production for binaryy expr without left operand
primary         -> NUMBER | STRING | "true" | "false" | "nil" | "(" expression ")" | IDENTIFIER ;